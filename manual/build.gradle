buildscript {
	repositories {
		mavenRepo (name: "Alkemist's GitHub", url: "http://cloud.github.com/downloads/alkemist") {
			pattern = "[organisation]/[module]-[revision].[ext]"
		}
		mavenCentral()
	}
	dependencies {
		classpath "markdown2book:markdown2book:1.0-SNAPSHOT"
	}
}

task tokeniseManual(type: Copy) {
	from "src"
	into "$buildDir/manual-tokenised"

	ext.substitutionProperties = [
		"gftt-version": project.version,
		"created-at": new java.text.SimpleDateFormat("MMMM, yyyy").format(new Date()),
		"gaelyk-remote-version": project.gaelykRemoteVersion,
		"spock-version": project.spockVersion,
		"geb-version": project.gebVersion,
		"gae-geb-plugin-version": project.gaeGebPluginVersion
	]

	inputs.properties(substitutionProperties)

	def tokenisableExtensions = ["md", "html"]
	inputs.property("tokenisableExtensions", tokenisableExtensions)

	eachFile { file ->
		if (tokenisableExtensions.any { file.name.endsWith(it) }) {
			file.filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: substitutionProperties)
		}
	}
}

task compileManual {
	dependsOn tokeniseManual

	ext.source = tokeniseManual.destinationDir
	inputs.dir(source)

	ext.destination = file("$buildDir/manual-compiled")
	outputs.dir(destination)

	ext.encoding = "UTF-8"
	inputs.property("encoding", encoding)

	doLast {
		new markdown2book.Generator(source, destination, encoding).generate()
	}
}